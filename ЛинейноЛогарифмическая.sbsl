
/*
Линейно-логарифмическая сложность
O(n log n) означает, что время выполнения алгоритма растёт пропорционально произведению:
количества элементов n и логарифма от этого количества log n (обычно по основанию 2, но в Big O основание не важно).
Это быстрее, чем квадратичная O(n²), но медленнее, чем линейная O(n)
*/

метод ПолучитьМассивСлучайныхЧисел(КоличествоЭлементов: Число): Массив<Число>
    знч МассивРезультат = <Число>[]
    знч ГенераторЧисла = новый СлучайныйГенератор()
    для Индекс = 1 по КоличествоЭлементов 
        МассивРезультат.Добавить(ГенераторЧисла.СлучайноеЧисло(0,10000).Округлить())    
    ; 
    возврат МассивРезультат   
;


// Функция слияния двух отсортированных массивов
метод Слить(ЛевыйМассив: Массив<Число>, ПравыйМассив: Массив<Число>): Массив<Число>
    
    знч Результат = <Число>[]
    пер ИндексЛевый = 0
    пер ИндексПравый = 0
    
    // Проходим оба массива, пока оба не закончились
    пока (ИндексЛевый < ЛевыйМассив.Размер() и ИндексПравый < ПравыйМассив.Размер())
        если (ЛевыйМассив[ИндексЛевый] <= ПравыйМассив[ИндексПравый])
            Результат.Добавить(ЛевыйМассив[ИндексЛевый])
            ИндексЛевый += 1
        иначе
            Результат.Добавить(ПравыйМассив[ИндексПравый])
            ИндексПравый += 1
        ;
    ;

    // Добавляем оставшиеся элементы левого массива
    пока (ИндексЛевый < ЛевыйМассив.Размер())
        Результат.Добавить(ЛевыйМассив[ИндексЛевый])
        ИндексЛевый += 1
    ;

    // Добавляем оставшиеся элементы правого массива
    пока (ИндексПравый < ПравыйМассив.Размер())
        Результат.Добавить(ПравыйМассив[ИндексПравый])
        ИндексПравый += 1
    ;

    возврат Результат
;

// Рекурсивная функция сортировки слиянием
метод СортировкаСлиянием(ИсходныйМассив: Массив<Число>): Массив<Число>
    если (ИсходныйМассив.Размер() <= 1) 
        возврат ИсходныйМассив
    ;

    пер СерединаМассива = (ИсходныйМассив.Размер() / 2).ЦелаяЧасть()
    знч ЛевыйМассив = СортировкаСлиянием(ИсходныйМассив.ПодМассив(0, СерединаМассива))
    знч ПравыйМассив = СортировкаСлиянием(ИсходныйМассив.ПодМассив(СерединаМассива))

    возврат Слить(ЛевыйМассив, ПравыйМассив)
;

метод Скрипт()
    
    пер НачалоОперации = ДатаВремя.Сейчас()
    знч ИсходныйМассив10 = ПолучитьМассивСлучайныхЧисел(10)
    знч ОтсортированныйМассив10 = СортировкаСлиянием(ИсходныйМассив10)
    пер ДлительностьОперации = (ДатаВремя.Сейчас() - НачалоОперации).ВМиллисекундах()  
    Консоль.Записать("Отсортированный массив: %ОтсортированныйМассив10") 
    Консоль.Записать("Длительность: %ДлительностьОперации мс")
    
    НачалоОперации = ДатаВремя.Сейчас()
    знч ИсходныйМассив1000 = ПолучитьМассивСлучайныхЧисел(1000)
    знч ОтсортированныйМассив1000 = СортировкаСлиянием(ИсходныйМассив1000)
    ДлительностьОперации = (ДатаВремя.Сейчас() - НачалоОперации).ВМиллисекундах()  
    Консоль.Записать("Отсортированный массив: %{ОтсортированныйМассив1000.Представление().Обрезать(50,"...]")}")
    Консоль.Записать("Длительность: %ДлительностьОперации мс") 
;



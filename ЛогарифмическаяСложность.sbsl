/* Когда говорят, что алгоритм работает за O(log n), это значит:
количество шагов алгоритма растёт медленнее, чем размер входных данных.
Если при увеличении входа в 2 раза шагов прибавляется всего на 1, это признак логарифма */

метод ПолучитьСортированныйМассивСлучайныхЧисел(КоличествоЭлементов: Число): Массив<Число>
    знч МассивРезультат = <Число>[]
    знч ГенераторЧисла = новый СлучайныйГенератор()
    для Индекс = 1 по КоличествоЭлементов 
        МассивРезультат.Добавить(ГенераторЧисла.СлучайноеЧисло(0,1000000).Округлить())    
    ; 
    возврат МассивРезультат.СортироватьПо(Элемент -> Элемент)   
;

метод Скрипт()
    ЛогарифмическаяСложность()    
;

метод ЛогарифмическаяСложность()   
    знч Массив100 = ПолучитьСортированныйМассивСлучайныхЧисел(КоличествоЭлементов = 100)
    знч Массив1000000 = ПолучитьСортированныйМассивСлучайныхЧисел(КоличествоЭлементов = 1000000)
    
    пер Результат = БинарныйПоискВМассиве(Массив100, Массив100.Получить(50))
    Консоль.Записать("Поиск по массиву 100 элементов. Индекс найденного элемента: %{Результат.Первый().Ключ}, количество итераций: %{Результат.Первый().Значение}")
    
    Результат = БинарныйПоискВМассиве(Массив1000000, Массив1000000.Получить(5000))
    Консоль.Записать("Поиск по массиву 10 000 элементов. Индекс найденного элемента: %{Результат.Первый().Ключ}, количество итераций: %{Результат.Первый().Значение}")  
;

метод БинарныйПоискВМассиве(МассивПоиска: Массив<Число>, ЗначениеПоиска: Число): Соответствие<Число, Число>
    пер ЛеваяГраницаПоиска = 0
    пер ПраваяГраницаПоиска = МассивПоиска.Граница()
    пер КоличествоИтераций = 0
    пока ЛеваяГраницаПоиска <= ПраваяГраницаПоиска
        КоличествоИтераций += 1
        пер СреднийЭлементИндекс = ((ЛеваяГраницаПоиска + ПраваяГраницаПоиска) / 2).ЦелаяЧасть() 
        знч СреднийЭлементЗначение = МассивПоиска.Получить(СреднийЭлементИндекс)
        если СреднийЭлементЗначение == ЗначениеПоиска
            возврат {СреднийЭлементИндекс: КоличествоИтераций}
        иначе если СреднийЭлементЗначение < ЗначениеПоиска
            ЛеваяГраницаПоиска = СреднийЭлементИндекс + 1
        иначе
            ПраваяГраницаПоиска = СреднийЭлементИндекс - 1
        ; 
    ;     
    возврат {-1: КоличествоИтераций} // Не нашли значение в массиве   
;
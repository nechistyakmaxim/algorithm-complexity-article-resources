/*
Факториальная сложность встречается, когда количество операций растёт как n! (n факториал).
Например:
n = 3 → 3! = 6 операций
n = 5 → 5! = 120 операций
n = 10 → 10! = 3 628 800 операций
Это означает, что даже маленькое увеличение n резко увеличивает время работы алгоритма.
Обычно такая сложность встречается при:
- Перестановках элементов массива
- Полном переборе вариантов задач типа "коммивояжёр"
- Расстановке объектов во всех возможных комбинациях
 */
 
метод ПолучитьМассивСлучайныхЧисел(КоличествоЭлементов: Число): Массив<Число>
    знч МассивРезультат = <Число>[]
    знч ГенераторЧисла = новый СлучайныйГенератор()
    для Индекс = 1 по КоличествоЭлементов 
        МассивРезультат.Добавить(ГенераторЧисла.СлучайноеЧисло(0,100).Округлить())    
    ; 
    возврат МассивРезультат   
; 
 
метод Скрипт()
    ФакториальнаяСложность()
;

метод ФакториальнаяСложность()
    для РазмерМассива = 2 по 8 шаг 2
        знч ИсходныйМассив = ПолучитьМассивСлучайныхЧисел(РазмерМассива)
        знч МассивРезультаты = Перестановки(ИсходныйМассив)
        Консоль.Записать("Количество перестановок: ${МассивРезультаты.Размер()}")
    ;
;



// Метод перебора перестановок
метод ПереборПерестановок(ТекущаяРасстановка: Массив<Число>, Оставшиеся: Массив<Число>, Результат: Массив<Строка>) 
    если Оставшиеся.Пусто() 
        // Преобразуем текущую перестановку в строку через запятую и добавляем в результат
        Результат.Добавить(ТекущаяРасстановка.Представление())
        возврат
    ;

    для Индекс = 0 по Оставшиеся.Размер() - 1 
        знч Элемент = Оставшиеся[Индекс]

        // Создаём новую текущую перестановку
        знч НоваяТекущая = <Число>[]
        НоваяТекущая.ДобавитьВсе(ТекущаяРасстановка)
        НоваяТекущая.Добавить(Элемент)

        // Создаём массив оставшихся без текущего элемента
        знч НовыеОставшиеся = <Число>[]
        для i = 0 по Оставшиеся.Размер() - 1
            если i != Индекс 
                НовыеОставшиеся.Добавить(Оставшиеся[i])
            ;
        ;

        // Рекурсивный вызов
        ПереборПерестановок(НоваяТекущая, НовыеОставшиеся, Результат)
    ;
;

// Метод генерации всех перестановок
метод Перестановки(ИсходныйМассив: Массив<Число>): Массив<Строка> 
    знч Результат = <Строка>[]
    ПереборПерестановок(новый Массив<Число>(), ИсходныйМассив, Результат)
    возврат Результат
;